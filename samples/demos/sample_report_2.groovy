import edu.uvm.banner.ScriptTool;
@groovy.transform.BaseScript ScriptTool scriptTool

    // Get parms
    def parms = new parms()
    parms.hold_code = input("HoldCD",'FH').toUpperCase()
    parms.allHolds = input("All Holds",'Y',ck.isInList.rcurry(['Y','y','N','n'])).toUpperCase()
    parms.aidy_code = input("Aid Year",'1617', ck.isAIDY)
    parms.termCode = input("Term",'201609', ck.isTermCD)

    //Setup Report definition 
    rpt.addHead( new Date().format('MM/dd/yyyy h:mm a') , "University of Vermont",{"Page " + delegate.pgno})
    .addHead("", "Hold Report","")
    .addFoot("", "Generated by ${username} at ${dbname}", "") 
    .addColHead( 10, 'L',"%-10s",    ["ID"])
    .addColHead( 24, 'L',"%-24s",    ["Name"])
    .addColHead( 11, 'R',"%,10.2f ", ["Acct Bal "])
    .addColHead(  8, 'L',"%-8s",     ["Hold"])
    .addColHead( 11, 'R',"%,10.2f ", ["Amount "])
    .addColHead(  4, 'L',"%-4s",     ["FAF"])
    .addColHead(  4, 'L',"%-4s",     ["Lvl"])
    .addColHead( 12, 'L',"%-12s",    ["From Date"])
    .addColHead(  4, 'L',"%-4s",     ["Cls"])
    .addColHead(  3, 'L',"%-3s",     ["Sts"])
    rpt.lpp=51;rpt.cpl=rpt.colHeaders.sum {it.width}

    //Main Program Loop
    int acct_cnt = 0, hold_cnt = 0, prev_pidm = 0 
    sql.eachRow(queries.sqlMain, [parms]){ row ->
        parms.pidm = row.spriden_pidm
        def holds = sql.rows(queries.sqlHolds, [parms])
        def lvl = sql.firstRow(queries.sqlLevel, [parms])
        def fafsa = sql.firstRow(queries.sqlFafsa, [parms])
        def cls = sql.firstRow(queries.sqlClass, [parms])

        def stuname = row.spriden_last_name + ", " + row.spriden_first_name + " " + (row.spriden_mi !=  null ? row.spriden_mi : "") 
        stuname = stuname.substring(0,[24,stuname.size()].min())
        acct_cnt ++
        hold_cnt += holds.size()
        //Output the Row
        holds.each {h ->  
            boolean bRow1 = prev_pidm != row.spriden_pidm
            prev_pidm = row.spriden_pidm
            rpt.pl( bRow1 ? row.spriden_id : " ",
                    bRow1 ? stuname : " ",
                    bRow1 ? row.balance : '',
                    h.sprhold_hldd_code,
                    h.sprhold_amount_owed,
                    fafsa?.robusdf_value_29,
                    lvl?.sgbstdn_levl_code,
                    h.fromdate,
                    cls?.swxclas_code,
                    lvl?.sgbstdn_stst_code
                  )
        }   
    }   
    // Print summary Stats
    rpt.pl( "Accounts: ${acct_cnt}" )
    rpt.pl( "Holds:    ${hold_cnt}" )
//-----------------------------------------------//
class parms {
    def hold_code 
    def allHolds
    def aidy_code
    def termCode
    def pidm
}
class queries{
    //Define Queries to be run
    static String sqlMain = """ 
        select spriden_pidm, spriden_id, spriden_last_name, spriden_first_name, spriden_mi, sum(tbraccd_balance) balance
         from spriden, tbraccd, sprhold
        where sprhold_pidm = spriden_pidm
          and sprhold_hldd_code = :hold_code
          and sprhold_to_date >= sysdate
          and tbraccd_pidm = sprhold_pidm
          and spriden_change_ind is null
          and spriden_entity_ind = 'P'
          --and spriden_pidm in (247944, 1904161, 389799, 390033)
        group by spriden_pidm, spriden_id, spriden_last_name, spriden_first_name,
          spriden_mi, sprhold_from_date
        order by lower(spriden_last_name), spriden_first_name
    """

    static String sqlHolds = """ 
        select sprhold_hldd_code, sprhold_amount_owed, to_char(sprhold_from_date,'DD-MON-YYYY') fromdate
        from sprhold where sprhold_pidm = :pidm and sprhold_to_date >= sysdate
        and (:allHolds = 'Y' or sprhold_hldd_code = :hold_code)
        order by sprhold_hldd_code
    """

    static String sqlLevel = """ 
    select sgbstdn_levl_code, sgbstdn_stst_code
    from sgbstdn where sgbstdn_pidm = :pidm                                                                                                                              
      and sgbstdn_term_code_eff = (select max(sgbstdn_term_code_eff) 
                                from sgbstdn where sgbstdn_pidm = :pidm)
    """

    static String sqlFafsa = """
        select robusdf_value_29 from robusdf 
        where robusdf_pidm = :pidm and robusdf_aidy_code = :aidy_code
    """

    static String sqlClass = """
        select swxclas_code from  swxclas
        where swxclas_code = swf_clas_calc(:pidm, :termCode)
    """
}                                                                            
