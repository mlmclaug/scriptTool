! ========================================================================
! file : ar_all_holds_.sqr
! desc : SQR script to list all active holds for all students having a
!        specified active hold.
! auth : Bernadette Gratton, CIT/Admin Info Services
! date : January 18, 2000
!      : JAC August 4, 2003 Removed MPP hold info.
!      : JZT Sept. 27, 2006 Removed expiration date from the report.
!                           Added Level Code and FAFSA.
!
! ========================================================================

begin-heading 4
  date-time   (1,1)  'MM/DD/YYYY HH24:MI'
  page-number (1,70) 'Page '
  print 'Student Accounting Office' (1) center
  print $db_type   (2,1)
  print $title     (2) center
  print $user      (2,70)
  print 'ID'       (4,1)
  print 'Name'     (,11) 
  print 'Acct Bal' (,38)
  print 'Hold'     (,48)
  print '  Amount' (,56)
  print 'Lvl'      (,66)
  print 'FAF'      (,70)
end-heading

begin-report
  do init_routine
  show ''
  show 'List All Active Holds For Students Having Active Specified Hold'
  show ''
  show 'date: ' &date ' ' &time
  do get_db_type
  do get_user
  show ''
  do get_parms
  do process_records
end-report

begin-setup
  page-size 59 81
end-setup

! -----------------------------------------------------------------------------
begin-procedure init_routine
! -----------------------------------------------------------------------------
  date-time () DD-MON-YYYY &date
  date-time () HH:MIPM     &time
  move 0 to #acct_cnt
  move 0 to #hold_cnt
end-procedure

! -----------------------------------------------------------------------------
begin-procedure get_parms
! -----------------------------------------------------------------------------
  input $hold_code 'Enter the hold code'
  input $aidy_code 'Enter Academic Year, For Instance, 0607'
  uppercase $hold_code
  let $title = 
      'List All Active Holds For Students Having Active '||$hold_code||' Hold'
end-procedure

! -----------------------------------------------------------------------------
begin-procedure get_db_type
! -----------------------------------------------------------------------------
begin-select
value &db_type
  let $db_type = &db_type
  show 'database: ' $db_type
 from sys.v_$parameter
where name = 'db_name'
end-select
end-procedure

! -----------------------------------------------------------------------------
begin-procedure get_user
! -----------------------------------------------------------------------------
begin-select
username
   move &username to $user
 from v$session
where username = user
end-select
end-procedure

! -----------------------------------------------------------------------
begin-procedure process_records
! -----------------------------------------------------------------------------
begin-select
spriden_pidm         &pidm
spriden_id           &id
spriden_last_name    &last_name
spriden_first_name   &first_name
spriden_mi           &mi
sum(tbraccd_balance) &balance
!to_char(sprhold_to_date,'DD-MON-YYYY')      &exp_date

   let $name = substr(&last_name||', '||&first_name||' '||&mi,1,25)
   add 1 to #acct_cnt

   print &id       (+1,1)
   print $name     (,11)
   print &balance  (,37) edit 99,999.99
   do get_level
   do get_fafsa
   do get_holds

 from spriden, tbraccd, sprhold
where sprhold_pidm = spriden_pidm
  and sprhold_hldd_code = $hold_code
  and sprhold_to_date >= sysdate
  and tbraccd_pidm = sprhold_pidm
  and spriden_change_ind is null
group by spriden_pidm, spriden_id, spriden_last_name, spriden_first_name,
  spriden_mi, sprhold_to_date
order by lower(spriden_last_name), spriden_first_name
end-select
  print 'Accounts: ' (+2,1)
  print #acct_cnt    () edit 9,999
  print 'Holds:    ' (+1,1)
  print #hold_cnt    () edit 9,999
end-procedure


! -----------------------------------------------------------------------------
begin-procedure get_holds
! -----------------------------------------------------------------------------
  move 0 to #tmp_cnt
begin-select
sprhold_hldd_code    &hold
sprhold_amount_owed  &hold_amt
! to_char(sprhold_to_date,'DD-MON-YYYY')      &exp_date

  if #tmp_cnt = 0
     print &hold  (,49)
  else
     print &hold  (+1,49)
     print &level (,66)
     print &fafsa (,70)
  end-if
  print &hold_amt (,56) edit 99,999.99
! print &exp_date (,66)
  add 1 to #tmp_cnt
     
 from sprhold
where sprhold_pidm = &pidm
  and sprhold_to_date >= sysdate
order by sprhold_hldd_code
end-select
  add #tmp_cnt to #hold_cnt 
end-procedure

! -----------------------------------------------------------------------------
begin-procedure get_level
! -----------------------------------------------------------------------------
begin-select
sgbstdn_levl_code    &level

  print &level    (,66)
     
 from sgbstdn
where sgbstdn_pidm = &pidm
  and sgbstdn_term_code_eff = (select max(sgbstdn_term_code_eff) from sgbstdn
                               where  sgbstdn_pidm = &pidm)
end-select
end-procedure


! -----------------------------------------------------------------------------
begin-procedure get_fafsa
! -----------------------------------------------------------------------------
begin-select
robusdf_value_29     &fafsa

  print &fafsa    (,70)
     
 from robusdf
where robusdf_pidm = &pidm
  and robusdf_aidy_code = $aidy_code
end-select
end-procedure



























































! end-of-file
